#!/usr/bin/env raku
use v6.d;

use Graphviz::DOT::Grammar;

my %*SUB-MAIN-OPTS = :named-anywhere;

#-----------------------------------------------------------

#| Converts Graphviz DOT language texts or files into Mermaid-JS, Mathematica, Markdown, JSON, or SVG files.
sub MAIN(Str $text is copy,          #= Input file or DOT spec.
         Str :t(:$to) = 'Whatever',  #= Format to convert to. (One of 'json', 'mathematica', 'mermaid', 'svg', or 'Whatever'.)
         Str :o(:$output) = '',      #= Output file; if an empty string then the result is printed to stdout.
         ) {

    $text = slurp($text) if $text.IO.f;

    my $toSpec = $to;
    if !$to || $to.lc âˆˆ <whatever automatic> {
        $toSpec = do given $output {
            when !$_.chars { 'mermaid' }
            when $_ ~~ / '.wl' | '.m' / { 'mathematica' }
            when $_ ~~ / '.dot' / { 'dot' }
            when $_ ~~ / '.json' / { 'json' }
            when $_ ~~ / '.svg' / { 'svg' }
            when $_ ~~ / '.eps' / { '.eps' }
            when $_ ~~ / '.md' | '.mermaid' / { 'mermaid' }
            when $_ ~~ / '.raku' | '.p6' / { 'raku' }
            default {
                note 'Cannot automatically determine the format to convert to.';
                'mermaid'
            }
        }
    }

    my $res = dot-interpret($text, actions => $toSpec);

    if $output.chars > 0 {
        spurt($output, $res);
    } else {
        say $res;
    }
}